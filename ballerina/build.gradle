/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        maven {
            url = 'https://maven.pkg.github.com/ballerina-platform/plugin-gradle'
            credentials {
                username System.getenv("packageUser")
                password System.getenv("packagePAT")
            }
        }
    }
    dependencies {
        classpath "io.ballerina:plugin-gradle:${project.ballerinaGradlePluginVersion}"
    }
}

description = 'Ballerina - OS Ballerina Generator'

def packageName = "os"
def packageOrg = "ballerina"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")
def configTOMLFile = new File("$project.projectDir/tests/Config.toml")
def originalConfigTOMLFile = configTOMLFile.text

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

configurations {
    externalJars
}

dependencies {
    externalJars(group: 'io.ballerina.stdlib', name: 'io-native', version: "${stdlibIoVersion}") {
        transitive = false
    }
    externalJars(group: 'org.burningwave', name: 'core', version: "${burningwaveCoreVersion}") {
        transitive = false
    }
    externalJars(group: 'io.github.toolfactory', name: 'jvm-driver', version: "${toolfactoryJvmDriverVersion}") {
        transitive = false
    }
}

apply plugin: 'io.ballerina.plugin'

ballerina {
    packageOrganization = packageOrg
    module = packageName
    langVersion = ballerinaLangVersion
}

task updateTomlFiles {
    doLast {
        def stdlibNativeIoVersion = project.stdlibIoVersion
        def stdlibIoVersion = stripBallerinaExtensionVersion("${stdlibNativeIoVersion}")

        def newConfig = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        def distributionBinPath =  "$project.rootDir/target/ballerina-runtime/bin"
        newConfig = newConfig.replace("@toml.version@", tomlVersion)
        newConfig = newConfig.replace("@io.native.version@", stdlibNativeIoVersion)
        newConfig = newConfig.replace("@io.version@", stdlibIoVersion)
        newConfig = newConfig.replace("@burningwave.core.version@", burningwaveCoreVersion)
        newConfig = newConfig.replace("@toolfactory.jvmdriver.version@", toolfactoryJvmDriverVersion)
        ballerinaTomlFile.text = newConfig
        setExecPath(configTOMLFile,distributionBinPath)
    }
}

def setExecPath(configTOMLFile, distributionBinPath) {
    def distributionPath
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        distributionPath = (distributionBinPath + "/bal.bat").replace("/", "\\")
        distributionPath = distributionPath.replace("\\", "\\\\")
    } else {
        distributionPath = distributionBinPath + "/bal"
    }
    configTOMLFile.text = configTOMLFile.text.replace("@exec.path@", distributionPath)
}

task commitTomlFiles {
    doLast {
        configTOMLFile.text = originalConfigTOMLFile
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the native jar versions\" Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the native jar versions' Ballerina.toml Dependencies.toml"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-${packageOrg}-${packageName}")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

updateTomlFiles.dependsOn copyStdlibs

build.dependsOn "generatePomFileForMavenPublication"
build.dependsOn ":${packageName}-native:build"
build.dependsOn ":${packageName}-test-utils:build"
test.dependsOn ":${packageName}-native:build"
test.dependsOn ":${packageName}-test-utils:build"

publishToMavenLocal.dependsOn build
publish.dependsOn build
